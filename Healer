//===== rAthena Script =======================================
//= Healer
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Basic healer script.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Aligned coordinates with @go.
//============================================================
prontera,151,187,5	script	Healer	742,{
	mes "[Healer]";
	mes "I have amazing healing powers!!";
	mes "You look like a person who needs them";
	mes "Want some?";
	next;
	if (select("Heal:No thanks") == 2) {
		mes "[Healer]";
		mes "Allright, come back if you need a heal.";
		end;
	}
	set .@Price,0;	// Zeny required for heal
	set .@Buffs,0;	// Also buff players? (1: yes / 0: no)
	set .@Delay,0;	// Heal delay, in seconds

	if (@HD > gettimetick(2)) end;
	if (.@Price) {
		message strcharinfo(0),"Healing costs "+callfunc("F_InsertComma",.@Price)+" Zeny.";
		if (Zeny < .@Price) end;
		if(select("^0055FFHeal^000000:^777777Cancel^000000") == 2) end;
		set Zeny, Zeny-.@Price;
	}
	specialeffect2 EF_HEAL2; percentheal 100,100;
	if (.@Buffs) {
		specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
		specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	}
	if (.@Delay) set @HD, gettimetick(2)+.@Delay;
	end;
}
// Duplicates
//============================================================
alberta,25,240,6	duplicate(Healer)	Healer#alb	742
aldebaran,135,118,6	duplicate(Healer)	Healer#alde	742
amatsu,200,79,4	duplicate(Healer)	Healer#ama	742
ayothaya,207,169,6	duplicate(Healer)	Healer#ayo	742
comodo,184,158,6	duplicate(Healer)	Healer#com	742
einbech,57,36,6	duplicate(Healer)	Healer#einbe	742
einbroch,57,202,6	duplicate(Healer)	Healer#einbr	742
geffen,115,72,6	duplicate(Healer)	Healer#gef	742
gonryun,156,122,6	duplicate(Healer)	Healer#gon	742
harboro1,283,195,4	duplicate(Healer)	Healer#har	742
hugel,89,150,6	duplicate(Healer)	Healer#hug	742
izlude,121,150,6	duplicate(Healer)	Healer#izl	742	//Pre-RE: (125,118)
jawaii,250,139,4	duplicate(Healer)	Healer#jaw	742
lighthalzen,152,100,6	duplicate(Healer)	Healer#lhz	742
louyang,226,103,4	duplicate(Healer)	Healer#lou	742
manuk,272,144,6	duplicate(Healer)	Healer#man	742
mid_camp,203,289,6	duplicate(Healer)	Healer#mid	742
moc_ruins,72,164,4	duplicate(Healer)	Healer#moc	742
morocc,153,97,6	duplicate(Healer)	Healer#mor	742
moscovia,220,191,4	duplicate(Healer)	Healer#mos	742
niflheim,212,182,5	duplicate(Healer)	Healer#nif	742
payon,179,106,4	duplicate(Healer)	Healer#pay	742
rachel,125,116,6	duplicate(Healer)	Healer#rac	742
splendide,201,153,4	duplicate(Healer)	Healer#spl	742
thor_camp,249,74,4	duplicate(Healer)	Healer#thor	742
umbala,105,148,3	duplicate(Healer)	Healer#umb	742
veins,217,121,4	duplicate(Healer)	Healer#ve	742
xmas,143,136,4	duplicate(Healer)	Healer#xmas	742
yuno,164,45,4	duplicate(Healer)	Healer#yuno	742

// Duplicates (Renewal)
//============================================================
brasilis,194,221,6	duplicate(Healer)	Healer#bra	742
dewata,195,187,4	duplicate(Healer)	Healer#dew	742
dicastes01,201,194,4	duplicate(Healer)	Healer#dic	742
ecl_in01,45,60,4	duplicate(Healer)	Healer#ecl	742
malangdo,132,114,6	duplicate(Healer)	Healer#mal	742
malaya,227,204,6	duplicate(Healer)	Healer#ma	742
mora,55,152,4	duplicate(Healer)	Healer#mora	742
